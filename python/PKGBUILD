shopt -s extglob

pkgbase=python+clang
pkgname=(python+clang python-tests+clang)
pkgver=3.10.2
pkgrel=2
_pybasever=${pkgver%.*}
pkgdesc="Next generation of the python high-level scripting language"
arch=('x86_64')
license=('custom')
url="https://www.python.org/"
depends=('bzip2' 'expat' 'gdbm' 'libffi' 'libnsl' 'libxcrypt' 'openssl' 'zlib')
makedepends=('tk' 'sqlite' 'bluez-libs' 'mpdecimal' 'llvm-all' 'gdb' 'ttf-font')
source=("https://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz"{,.asc})
sha256sums=('17de3ac7da9f2519aa9d64378c603a73a0e9ad58dffa8812e45160c086de64c7'
  'SKIP')
validpgpkeys=(
  '0D96DF4D4110E5C43FBFB17F2D347EA6AA65421D' # Ned Deily (Python release signing key) <nad@python.org>
  'E3FF2839C048B25C084DEBE9B26995E310250568' # ≈Åukasz Langa (GPG langa.pl) <lukasz@langa.pl>
  'A035C8C19219BA821ECEA86B64E628F8D684696D' # Pablo Galindo Salgado <pablogsal@gmail.com>
)

_llvm=1

prepare() {

  cd "${srcdir:?}/Python-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/Python-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  # FS#23997
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

  # Ensure that we are using the system copy of various libraries (expat, libffi, and libmpdec),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/_ctypes/{darwin,libffi}*
  rm -r Modules/_decimal/libmpdec

}

build() {

  cd "${srcdir:?}/Python-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/Python-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  # Disable bundled pip & setuptools
  ./configure --prefix=/usr \
              --enable-shared \
              --with-computed-gotos \
              --enable-optimizations \
              --with-lto \
              --enable-ipv6 \
              --with-system-expat \
              --with-dbmliborder=gdbm:ndbm \
              --with-system-ffi \
              --with-system-libmpdec \
              --enable-loadable-sqlite-extensions \
              --without-ensurepip \
              --with-tzpath=/usr/share/zoneinfo

  LC_CTYPE=en_US.UTF-8 make regen-all EXTRA_CFLAGS="${CFLAGS}"
  LC_CTYPE=en_US.UTF-8 make EXTRA_CFLAGS="${CFLAGS}"

}

check() {

  # test_socket: test.test_socket.RDSTest.testPeek hangs https://bugs.python.org/issue35247
  # test_tk: tkinter.test.test_tkinter.test_colorchooser.DefaultRootTest hangs

  cd "${srcdir:?}/Python-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/Python-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  LD_LIBRARY_PATH="${srcdir:?}/Python-${pkgver}":${LD_LIBRARY_PATH} \
    LC_CTYPE=en_US.UTF-8 \
    "${srcdir:?}/Python-${pkgver}/python" -m test.regrtest -v -uall -x test_socket -x test_tk -x test_zlib

}

package_python+clang() {

  optdepends=(
    'python-setuptools'
    'python-pip'
    'sqlite'
    'mpdecimal: for decimal'
    'xz: for lzma'
    'tk: for tkinter'
  )
  provides=('python3' 'python' "python=${pkgver}")
  conflicts=('python')
  replaces=('python3')

  cd "${srcdir:?}/Python-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/Python-${pkgver} directory! Prepare python Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" EXTRA_CFLAGS="${CFLAGS}" install

  # Split tests
  rm -r "${pkgdir:?}/usr/lib/python${_pybasever}/"{test,ctypes/test,distutils/tests,idlelib/idle_test,lib2to3/tests,sqlite3/test,tkinter/test,unittest/test}

  # Why are these not done by default...
  ln -s python3 "${pkgdir:?}/usr/bin/python"
  ln -s python3-config "${pkgdir:?}/usr/bin/python-config"
  ln -s idle3 "${pkgdir:?}/usr/bin/idle"
  ln -s pydoc3 "${pkgdir:?}/usr/bin/pydoc"
  ln -s "python${_pybasever}.1" "${pkgdir:?}/usr/share/man/man1/python.1"

  # some useful "stuff" FS#46146
  install -dm755 "${pkgdir:?}/usr/lib/python${_pybasever}/Tools/"{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py "${pkgdir:?}/usr/lib/python${_pybasever}/Tools/i18n/"
  install -m755 Tools/scripts/{README,*py} "${pkgdir:?}/usr/lib/python${_pybasever}/Tools/scripts/"

  # License
  install -Dm644 LICENSE "${pkgdir:?}/usr/share/licenses/python/LICENSE"

}

package_python-tests+clang() {

  pkgdesc="Regression tests packages for Python"
  depends=('python')

  cd "${srcdir:?}/Python-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/Python-${pkgver} directory! Package python-tests Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" EXTRA_CFLAGS="${CFLAGS}" libinstall

  cd "${pkgdir:?}/usr/lib/python${_pybasever}/" &&
    rm -r !(test|ctypes|distutils|idlelib|lib2to3|sqlite3|tkinter|unittest)
  cd "${pkgdir:?}/usr/lib/python${_pybasever}/ctypes" &&
    rm -r !(test)
  cd "${pkgdir:?}/usr/lib/python${_pybasever}/distutils" &&
    rm -r !(tests)
  cd "${pkgdir:?}/usr/lib/python${_pybasever}/idlelib" &&
    rm -r !(idle_test)
  cd "${pkgdir:?}/usr/lib/python${_pybasever}/lib2to3" &&
    rm -r !(tests)
  cd "${pkgdir:?}/usr/lib/python${_pybasever}/sqlite3" &&
    rm -r !(test)
  cd "${pkgdir:?}/usr/lib/python${_pybasever}/tkinter" &&
    rm -r !(test)
  cd "${pkgdir:?}/usr/lib/python${_pybasever}/unittest" &&
    rm -r !(test)

}