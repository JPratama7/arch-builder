pkgname=xorg-xkbcomp+clang
pkgver=1.4.5
pkgrel=1
pkgdesc="X Keyboard description compiler"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
provides=('xorg-xkbcomp' "xorg-xkbcomp=${pkgver}")
conflicts=('xorg-xkbcomp')
depends=('libxkbfile')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=("${url}/archive/individual/app/xkbcomp-${pkgver}.tar.bz2"{,.sig})
sha512sums=('2e84f8d6b083e2e818cb95e5d2a184da00e50d71b280e92d1a71465a9a4348f922f4ddccb42cfeee6f76705bbfc8580ef67aedd0d958aaa1ae6bbcc66ccd8154'
  'SKIP')
validpgpkeys=(
  '3C2C43D9447D5938EF4551EBE23B7E70B467F0BF' # Peter Hutterer (Who-T) <office@who-t.net>
  'A66D805F7C9329B4C5D82767CCC4F07FAC641EFF' # "Daniel Stone <daniels@collabora.com>"
  'DD38563A8A8224537D1F90E45B8A2D50A0ECD0D3' # "Adam Jackson <ajax@benzedrine.nwnk.net>"
  '3BB639E56F861FA2E86505690FDD682D974CA72A' # "Matt Turner <mattst88@gmail.com>"
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/xkbcomp-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/xkbcomp-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr

  make

}

check() {

  cd "${srcdir:?}/xkbcomp-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/xkbcomp-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  make -k check

}

package() {

  cd "${srcdir:?}/xkbcomp-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/xkbcomp-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  install -m755 -d "${pkgdir:?}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
