# Based on https://gitlab.archlinux.org/archlinux/packaging/packages/aircrack-ng/-/raw/main/PKGBUILD
# Maintainer: envolution
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: ice-man <icemanf@gmail.com>
# Contributor: l-koehler <lorenz.koehler@posteo.de>
# Contributor: Josef Miegl <josef@miegl.cz>
# Contributor: Gaetan Bisson <bisson@archlinux.org
# Contributor: codyps <archlinux@codyps.com>

pkgname=aircrack-ng-git
pkgver=1.7+r4703+g13e5c460f
pkgrel=1
pkgdesc="Key cracker for the 802.11 WEP and WPA-PSK protocols"
arch=('i686' 'x86_64' 'aarch64' 'armv7h')
url="https://www.aircrack-ng.org"
license=('GPL2')
depends=('glibc' 'gcc-libs' 'openssl' 'sqlite' 'iw' 'net-tools' 'wireless_tools' 'ethtool'
         'pcre' 'libpcap' libpcap.so 'python' 'zlib' 'libnl' 'hwloc' 'usbutils')
optdepends=('python: needed for dump-join, airgraph-ng, versuck-ng, airdrop-ng'
            'usbutils: needed for airmon-ng'
            'pciutils: required for devices with populated PCI(e) bus'
            'gpsd: allows airodump-ng to log coordinates'
            'pcre2: SSID filtering with regular expressions in airodump-ng')
makedepends=('git' 'python-setuptools' 'autoconf')
checkdepends=('cmocka')
provides=('aircrack-ng-scripts')
conflicts=('aircrack-ng-scripts')
replaces=('aircrack-ng-scripts')
source=("git+https://github.com/aircrack-ng/aircrack-ng.git")
sha256sums=('SKIP')

pkgver() {
  cd aircrack-ng

  _version=$(git tag --sort=-v:refname --list | grep '^[0-9.]*$' | head -n1)
  _commits=$(git rev-list --count HEAD)
  _short_commit_hash=$(git rev-parse --short=9 HEAD)
  echo "${_version}+r${_commits}+g${_short_commit_hash}"
}

prepare() {
  cd aircrack-ng
  autoreconf -fiv
}

build() {
  cd aircrack-ng

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --sbindir=/usr/bin \
    --with-ext-scripts \
    --with-experimental

  make
}

check() {
  cd aircrack-ng
  make check
}

package() {
  cd aircrack-ng
  make DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et: