pkgname=ffmpeg
pkgver=5.1.2
pkgrel=1
epoch=2
pkgdesc='Complete solution to record, convert and stream audio and video'
arch=(x86_64)
url=https://ffmpeg.org/
license=(GPL3)
depends=(
  alsa-lib
  aom
  bzip2
  fontconfig
  fribidi
  gmp
  gnutls
  gsm
  jack
  lame
  libass.so
  libavc1394
  libbluray.so
  libbs2b.so
  libdav1d.so
  libdrm
  libfreetype.so
  libgl
  libiec61883
  libmfx
  libmodplug
  libpulse
  librav1e.so
  libraw1394
  librsvg-2.so
  libsoxr
  libssh
  libtheora
  libva.so
  libva-drm.so
  libva-x11.so
  libvdpau
  libvidstab.so
  libvorbisenc.so
  libvorbis.so
  libvpx.so
  libvulkan.so
  libwebp
  libx11
  libx264.so
  libx265.so
  libxcb
  libxext
  libxml2
  libxv
  libxvidcore.so
  libzimg.so
  ocl-icd
  opencore-amr
  openjpeg2
  opus
  sdl2
  speex
  srt
  svt-av1
  svt-hevc 
  svt-vp9
  v4l-utils
  vmaf
  xz
  zlib
)
makedepends=(
  amf-headers
  avisynthplus
  clang
  ffnvcodec-headers
  git
  ladspa
  mesa
  nasm
  opencl-headers
  vulkan-headers
)
optdepends=(
  'avisynthplus: AviSynthPlus support'
  'intel-media-sdk: Intel QuickSync support'
  'ladspa: LADSPA filters'
  'nvidia-utils: Nvidia NVDEC/NVENC support'
)
provides=(
  'ffmpeg' "ffmpeg=${epoch}:${pkgver}"
  libavcodec.so
  libavdevice.so
  libavfilter.so
  libavformat.so
  libavutil.so
  libpostproc.so
  libswresample.so
  libswscale.so
)
conflicts=('ffmpeg')
_svt_hevc_ver='b62f72e752243cee4104cfb41dc7ee409d3ac3e9'
_svt_vp9_ver='d9ef3cc13159143b9afc776c04f67cdfa6284046'
_commit=a6e26053c21362bb882932f3cfd1f1dfa2551f1d
source=(
  "git+http://git.ffmpeg.org/ffmpeg.git#commit=${_commit}"
  "040-ffmpeg-add-av_stream_get_first_dts-for-chromium.patch::https://aur.archlinux.org/cgit/aur.git/plain/040-ffmpeg-add-av_stream_get_first_dts-for-chromium.patch?h=ffmpeg-amd-full"
  "070-ffmpeg-fix-v4l2-memory-leak.patch::https://git.ffmpeg.org/gitweb/ffmpeg.git/patch/30aa0c3f4873a92c5e3da8ba8cf030de56bf4cf7"
  "010-ffmpeg-add-svt-hevc-g${_svt_hevc_ver:0:7}.patch::https://raw.githubusercontent.com/OpenVisualCloud/SVT-HEVC/${_svt_hevc_ver}/ffmpeg_plugin/master-0001-lavc-svt_hevc-add-libsvt-hevc-encoder-wrapper.patch"
  "030-ffmpeg-add-svt-vp9-g${_svt_vp9_ver:0:7}.patch::https://raw.githubusercontent.com/OpenVisualCloud/SVT-VP9/${_svt_vp9_ver}/ffmpeg_plugin/master-0001-Add-ability-for-ffmpeg-to-run-svt-vp9.patch"
  "060-ffmpeg-fix-segfault-with-avisynthplus.patch::https://aur.archlinux.org/cgit/aur.git/plain/060-ffmpeg-fix-segfault-with-avisynthplus.patch?h=ffmpeg-git"

)
sha256sums=('SKIP'
  '57e26caced5a1382cb639235f9555fc50e45e7bf8333f7c9ae3d49b3241d3f77')

_llvm=0

_patch() {

  echo -e "\E[1;33m Apply patch: ${1}\E[0m"
  patch -Np1 -i "${srcdir:?}/${1}"

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/ffmpeg" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/ffmpeg directory! Prepare Failed! \E[0m"
    exit 1
  )

 rm -f "ffmpeg-${pkgver}/libavcodec/"libsvt_{hevc,vp9}.c

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    _patch $src
  done

}

build() {

  cd "${srcdir:?}/ffmpeg" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/ffmpeg directory! Build Failed! \E[0m"
    exit 1
  )

  CFLAGS="${CFLAGS} -flto=thin"
  CXXFLAGS="${CXXFLAGS} -flto=thin"
  LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  ./configure \
    --prefix=/usr \
    --cc=/usr/bin/clang \
    --cxx=/usr/bin/clang++ \
    --host-cc=/usr/bin/clang \
    --disable-debug \
    --disable-static \
    --disable-stripping \
    --enable-amf \
    --enable-avisynth \
    --enable-cuda-llvm \
    --enable-lto \
    --enable-fontconfig \
    --enable-gmp \
    --enable-gnutls \
    --enable-gpl \
    --enable-ladspa \
    --enable-libaom \
    --enable-libass \
    --enable-libbluray \
    --enable-libbs2b \
    --enable-libdav1d \
    --enable-libdrm \
    --enable-libfreetype \
    --enable-libfribidi \
    --enable-libgsm \
    --enable-libiec61883 \
    --enable-libjack \
    --enable-libmfx \
    --enable-libmodplug \
    --enable-libmp3lame \
    --enable-libopencore_amrnb \
    --enable-libopencore_amrwb \
    --enable-libopenjpeg \
    --enable-libopus \
    --enable-libpulse \
    --enable-librav1e \
    --enable-librsvg \
    --enable-libsoxr \
    --enable-libspeex \
    --enable-libsrt \
    --enable-libssh \
    --enable-libsvtav1 \
    --enable-libtheora \
    --enable-libv4l2 \
    --enable-libvidstab \
    --enable-libvmaf \
    --enable-libvorbis \
    --enable-libvpx \
    --enable-libwebp \
    --enable-libx264 \
    --enable-libx265 \
    --enable-libxcb \
    --enable-libxml2 \
    --enable-libxvid \
    --enable-libzimg \
    --enable-nvdec \
    --enable-nvenc \
    --enable-opencl \
    --enable-opengl \
    --enable-shared \
    --enable-version3 \
    --enable-vulkan \
    \
    --enable-vaapi \
    --enable-vdpau \
    --enable-v4l2-m2m \
    --enable-libsvthevc \
    --enable-libsvtvp9 \
    --enable-nonfree \
    --enable-gray \

  make
  make tools/qt-faststart
  make doc/ff{mpeg,play}.1

}

package() {

  make DESTDIR="${pkgdir:?}" -C ffmpeg install install-man
  install -Dm 755 ffmpeg/tools/qt-faststart "${pkgdir:?}/usr/bin/"

}